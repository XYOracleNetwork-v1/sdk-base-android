apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'network.xyo'

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        google()
    }
}

Properties versionProps = new Properties()
def versionPropsFile = file('version.properties')

if (versionPropsFile.exists())
    versionProps.load(new FileInputStream(versionPropsFile))

def increment = 0

def runTasks = gradle.startParameter.taskNames

if (':base-android-library:assemble' in runTasks) {
    increment = 1
}

def majorVersion = 4
def minorVersion = 0
def patch = versionProps['VERSION_PATCH'].toInteger() + increment

def verString = majorVersion + '.' + minorVersion + '.' + patch

task printVersion {
    println verString
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    versionProps['VERSION_PATCH'] = patch.toString()
    versionProps.store(versionPropsFile.newWriter(), null)    

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        defaultPublishConfig 'release'
        versionCode project.majorVersion * 100000000 + project.minorVersion * 1000000 + project.patchVersion
        versionName project.majorVersion + "." + project.minorVersion + "." + project.patchVersion
    }

    lintOptions {
        htmlReport true
        abortOnError true
        warningsAsErrors true
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility "1.8"
        kotlinOptions {
            allWarningsAsErrors = true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/build.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude 'META-INF/library_release.kotlin_module'
    }
}

tasks.withType(Javadoc).all {
    enabled = false
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/base-android-library-release.aar")
            groupId 'network.xyo'
            artifactId 'sdk-base-android'
            version verString

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    dryRun = false
    override = false
    publish = true
    pkg {
        repo = 'xyo'
        name = 'sdk-base-android'
        userOrg = 'xyoraclenetwork'
        licenses = ['LGPL-3.0']
        desc = 'Android Base Classes'
        vcsUrl = 'https://github.com/xyoraclenetwork/sdk-base-android.git'

        version {
            name = verString
            vcsTag = verString
        }
    }
    publications = ['Production']
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    google()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.21'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.4.21'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2-native-mt'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.jaredrummler:android-device-names:1.1.8'
}
